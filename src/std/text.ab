/// Replaces all occurences of a pattern in the content with the provided replace text.
pub fun replace(source, search, replace) {
    return "\$\{source//{search}/{replace}}"
}

/// Replaces the first occurence of a pattern in the content with the provided replace text.
pub fun replace_once(source, search, replace) {
    return "\$\{source/{search}/{replace}}"
}

/// Replaces all occurences of a regex pattern in the content with the provided replace text.
///
/// Function uses `sed`
pub fun replace_regex(source: Text, search: Text, replace: Text, extended: Bool = false): Text {
    trust {
        search = replace(search, "/", "\/")
        replace = replace(replace, "/", "\/")
        if extended {
            // GNU sed versions 4.0 through 4.2 support extended regex syntax,
            // but only via the "-r" option; use that if the version information
            // contains "GNU sed".
            $ re='\bCopyright\b.+\bFree Software Foundation\b'; [[ \$(sed --version 2>/dev/null) =~ \$re ]] $
            let flag = status == 0 then "-r" else "-E"
            return $ echo "{source}" | sed {flag} -e "s/{search}/{replace}/g" $
        } else {
            return $ echo "{source}" | sed -e "s/{search}/{replace}/g" $
        }
    }
}

/// Splits the input `text` into an array of substrings using the specified `delimiter`.
pub fun split(text: Text, delimiter: Text): [Text] {
    let result = [Text]
    trust $ IFS="{delimiter}" read -rd '' -a {nameof result} < <(printf %s "\${nameof text}") $
    return result
}

/// Splits a `text` into an array of substrings based on newline characters.
pub fun split_lines(text: Text): [Text] {
    return split(text, "\n")
}

/// Splits a `text` into an array of substrings based on space character.
pub fun words(text: Text): [Text] {
    return split(text, " ")
}

/// Merges text using the delimeter specified.
pub fun join(list: [Text], delimiter: Text): Text {
    return trust $ IFS="{delimiter}" ; echo "\$\{{nameof list}[*]}" $
}

/// Trims the spaces at top of the text using `sed`.
pub fun trim_left(text: Text): Text {
    return trust $ echo "{text}" | sed -e 's/^[[:space:]]*//' $
}

/// Trims the spaces at end of the text using `sed`.
pub fun trim_right(text: Text): Text {
    return trust $ echo "{text}" | sed -e 's/[[:space:]]*\$//' $
}

/// Trims the spaces from the text input.
pub fun trim(text: Text): Text {
    return trim_left(trim_right(text))
}

/// Makes the text input lowercase using `tr`.
pub fun lower(text: Text): Text {
    return trust $ echo "{text}" | tr '[:upper:]' '[:lower:]' $
}

/// Makes the text input uppercase using `tr`.
pub fun upper(text: Text): Text {
    return trust $ echo "{text}" | tr '[:lower:]' '[:upper:]' $
}

/// Attempts to parse a given text into a number, returning the parsed number or zero if parsing fails.
#[allow_absurd_cast]
pub fun parse(text: Text): Num? {
    $ [ -n "{text}" ] && [ "{text}" -eq "{text}" ] 2>/dev/null $?
    return text as Num
}

/// Splits a text into an array of individual characters.
pub fun chars(text: Text): [Text] {
    let chars = [Text]
    trust $ for ((i=0; i<\$\{#{nameof text}}; i++)); do
        {nameof chars}+=( "\$\{{nameof text}:\$i:1}" );
    done $
    return chars
}

/// Checks if some text contains a value/
pub fun contains(text: Text, phrase: Text): Bool {
  let result = trust $ if [[ "{text}" == *"{phrase}"* ]]; then
    echo 1
  fi $

  return result == "1"
}

/// Reverses text using `rev`.
pub fun reverse(text: Text): Text {
    return trust $ echo "{text}" | rev $
}

/// Checks if text starts with a value.
pub fun starts_with(text: Text, prefix: Text): Bool {
  let result = trust $ if [[ "{text}" == "{prefix}"* ]]; then
    echo 1
  fi $

  return result == "1"
}

/// Checks if text ends with a value.
pub fun ends_with(text: Text, suffix: Text): Bool {
  let result = trust $ if [[ "{text}" == *"{suffix}" ]]; then
    echo 1
  fi $

  return result == "1"
}

/// Returns a substring from `text` starting at the given `index` (0-based).
///
/// If `index` is negative, the substring starts from the end of `text` based on the absolute value of `index`.
/// If `length` is provided, the substring will include `length` characters; otherwise, it slices to the end of `text`.
/// If `length` is negative, an empty string is returned.
pub fun slice(text: Text, index: Num, length: Num = 0): Text {
    if length == 0: length = len(text) - index
    if length <= 0: return ""
    return trust $ printf "%.{length}s" "\$\{text:{index}}" $
}

/// Returns the character from `text` at the specified `index` (0-based).
///
/// If `index` is negative, the substring starts from the end of `text` based on the absolute value of `index`.
pub fun char_at(text: Text, index: Num): Text {
    return trust $ printf "%.1s" "\$\{text:{index}}" $
}

/// Capitalize the first letter of the given `text`.
#[allow_absurd_cast]
pub fun capitalize(text: Text): Text {
    trust {
        if len(text) == 0: return text
        const bash_version = $ echo \"\$\{BASH_VERSINFO[0]}\" $ as Num
        if {
            bash_version >= 4 {
                return $ echo \"\$\{text^}\" $
            } else {
                // GNU sed supports \U
                $ re='\bCopyright\b.+\bFree Software Foundation\b'; [[ \$(sed --version 2>/dev/null) =~ \$re ]] $
                if status == 0:
                    return trust $ echo "{text}" | sed "s/^\(.\)/\U\1/" $
                else {
                    let first_letter = upper(char_at(text, 0)) 
                    return first_letter + slice(text, 1)
                }
            }
        }
    }
}

/// Pads `text` with the specified `pad` character on left until it reaches the desired `length`.
pub fun lpad(text: Text, pad: Text, length: Num): Text {
    if length <= len(text): return text
    length = len(text) - length
    pad = trust $ printf "%{length}s" "" | tr " " "{pad}" $
    return pad + text
}

/// Pads `text` with the specified `pad` character on the right until it reaches the desired `length`.
pub fun rpad(text: Text, pad: Text, length: Num): Text {
    if length <= len(text): return text
    length = len(text) - length
    pad = trust $ printf "%{length}s" "" | tr " " "{pad}" $
    return text + pad
}

/// Pads `text` with zeros on the left until it reaches the desired `length`.
pub fun zfill(text: Text, length: Num): Text {
    return lpad(text, "0", length)
}

main(args) {
    echo capitalize("aaa")
}
